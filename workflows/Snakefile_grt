import numpy as np
import pandas as pd

configfile: "config/config_grts.yaml"

def cartesian_product(*arrays):
    meshgrids = np.meshgrid(*arrays,indexing='ij')
    cart_prod = np.stack(meshgrids,axis=-1)
    cart_prod = cart_prod.reshape(-1,len(arrays))
    return cart_prod

def generate_samples(method_config):
    '''Generates the samples for the method
    '''
    samples = cartesian_product(eval(*method_config['parameters'].values()))
    samples = np.tile(samples,(method_config['n_graphs'],1))

    samples = pd.DataFrame(samples,columns=method_config['parameters'].keys())
    samples.rename(lambda idx: f"graph_{idx}", inplace=True)

    return samples

# Retrieve methods to analyze
methods = config['methods'].keys()

# Generate the samples for each method
samples = {method: generate_samples(config['methods'][method]) for method in methods}

rule all:
    input:
        # "results/giant_hamsterster/sparsification/graph_parameters.csv",
        # "results/giant_hamsterster/sparsification/graph_metrics.csv",
        # "results/giant_hamsterster/sparsification/graph_distribution_degree.csv",
        # "results/giant_hamsterster/sparsification/graph_distribution_distance.csv",
        # "results/giant_hamsterster/sparsification/sir_qois.csv"
        "results/giant_hamsterster/sparsification/plots/targets.png"

rule summarize_graph:
    input:
        files=lambda w: [f"data/networks/{w.graph}/{w.method}/{idx}/{w.dictionary}.yaml" for idx in samples[w.method].index]
    output:
        "results/{graph}/{method}/graph_{dictionary}.csv"
    script:
        "scripts/summarize.py"

rule summarize_sir:
    input:
        files=lambda w: [f"data/networks/{w.graph}/{w.method}/{idx}/sir_qois.yaml" for idx in samples[w.method].index]
    output:
        "results/{graph}/{method}/sir_qois.csv"
    script:
        "scripts/summarize_sir.py"

rule graph_reduce:
    input:
        "data/networks/{graph}/adjacency.npz",
    output:
        "data/networks/{graph}/{method}/{graph_idx}/adjacency.npz",
        "data/networks/{graph}/{method}/{graph_idx}/parameters.yaml"
    params:
        method_parameters=lambda w: samples[w.method].loc[w.graph_idx].to_dict()
    script:
        "scripts/graph_reduce.py"

rule graph_metrics:
    input:
        "{path}/adjacency.npz"
    output:
        "{path}/metrics.yaml"
    script:
        "scripts/graph_characterize.py"

rule graph_distribution:
    input:
        "{path}/adjacency.npz"
    output:
        "{path}/distribution_{quantity}.npy"
    script:
        "scripts/graph_distribution.py"

rule graph_distribution_metrics:
    input:
        "{path}/distribution_{quantity}.npy"
    output:
        "{path}/distribution_{quantity}.yaml"
    script:
        "scripts/graph_distribution_metrics.py"

rule model_simulate:
    input:
        "{path}/adjacency.npz",
    output:
        "{path}/trajectories_{model}.npy"
    params:
        config=lambda w:config['models'][w.model]
    script:
        "scripts/simulation_{wildcards.model}_run.py"

rule model_qois_sir:
    input:
        "{path}/trajectories_sir.npy"
    output:
        "{path}/sir_qois.yaml"
    script:
        "scripts/model_qois_sir.py"

rule plot_reduction:
    input:
        parameters="results/{graph}/{method}/graph_parameters.csv",
        metrics="results/{graph}/{method}/graph_metrics.csv",
        degree="results/{graph}/{method}/graph_distribution_degree.csv",
        distance="results/{graph}/{method}/graph_distribution_distance.csv"
    output:
        "results/{graph}/{method}/plots/targets.png"
    notebook:
        "notebooks/analysis_reduction.py.ipynb"

rule graph_draw:
    input:
        "{path}/adjacency.npz"
    output:
        "{path}/drawing.png"
    script:
        "scripts/graph_draw.py"
